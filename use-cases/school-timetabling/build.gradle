plugins {
    id "java"
    id "io.quarkus" version "2.2.0.Final"
}

def quarkusVersion = "2.2.0.Final"
def optaplannerVersion = "8.12.0-SNAPSHOT"

group = "org.acme"
version = "0.1.0-SNAPSHOT"

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://repository.jboss.org/nexus/content/groups/public/"
        mavenContent {
            snapshotsOnly()
        }
    }
}

dependencies {
    // Alternatively, use "io.quarkus:quarkus-universe-bom" which includes both quarkus-bom and optaplanner-bom.
    implementation enforcedPlatform("io.quarkus:quarkus-bom:${quarkusVersion}")
    implementation "org.optaplanner:optaplanner-quarkus:${optaplannerVersion}"
    implementation "org.optaplanner:optaplanner-quarkus-jackson:${optaplannerVersion}"
    implementation "org.optaplanner:optaplanner-quarkus-benchmark:${optaplannerVersion}"
    implementation "org.optaplanner:optaplanner-test:${optaplannerVersion}"
    implementation "io.quarkus:quarkus-resteasy"
    implementation "io.quarkus:quarkus-resteasy-jackson"
    implementation "io.quarkus:quarkus-hibernate-orm-panache"
    implementation "io.quarkus:quarkus-jdbc-h2"
    implementation "io.quarkus:quarkus-hibernate-orm-rest-data-panache"
    implementation "io.quarkus:quarkus-webjars-locator"
    runtimeOnly "org.webjars:bootstrap:4.3.1"
    runtimeOnly "org.webjars:jquery:3.4.1"
    runtimeOnly "org.webjars:font-awesome:5.11.2"
    runtimeOnly "org.webjars:momentjs:2.24.0"

    testImplementation "io.quarkus:quarkus-junit5"
    testImplementation "io.quarkus:quarkus-test-h2"
    testImplementation "io.rest-assured:rest-assured"
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs << "-parameters"
}

compileTestJava {
    options.encoding = "UTF-8"
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    // Gradle needs native tests in src/native-test/java, but maven needs them in src/test/java instead.
    // Maven first, so we skip them in Gradle unfortunately.
    exclude "**/**IT.class"
    // Log the test execution results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}
